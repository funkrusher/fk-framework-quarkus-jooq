plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'io.quarkus'
    id "org.liquibase.gradle"
}

dependencies {
    implementation enforcedPlatform(libs.quarkusPlatform)
    implementation project(':_databases:fk_database1')
    implementation project(':_databases:fk_database2')
    implementation project(':_modules:fk_root')
    implementation project(':_modules:fk_product')
}

task backend1Database1Liquibase(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = 'org.fk.core.liquibase.FkLiquibase'
    args '--databaseId', 'database1',
         '--changeLogFilename', 'database1/liquibase/changelog.xml',
         '--mode',  project.properties['rollbackTag'] ? 'rollback' : 'update',
            project.properties['rollbackTag'] ? project.properties['rollbackTag'] : ''
}

tasks.register('generatePartnerOpenApi') {
    group = 'openapi'
    description = 'Generate OpenAPI spec for partner with filtered endpoints'
    doLast {
        def gradlew = System.getProperty('os.name').toLowerCase().contains('windows') ? 'gradlew.bat' : './gradlew'
        exec {
            workingDir project.rootDir  // Ensure we are in the root directory
            commandLine gradlew, 'build', '-Dquarkus.profile=partnerapi'
        }
    }
}

// publishing
publishing {
    publications {
        maven(MavenPublication) {
            groupId = theGroup
            artifactId = "fk-backend1"
            version = theVersion
            from components.java
        }
    }
}