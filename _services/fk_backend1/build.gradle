plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'io.quarkus'
    id "org.liquibase.gradle"
}

dependencies {
    implementation enforcedPlatform(libs.quarkusPlatform)
    implementation project(':_databases:fk_database1')
    implementation project(':_databases:fk_database2')
    implementation project(':_modules:fk_root')
    implementation project(':_modules:fk_product')
    implementation project(':_modules:fk_library')
}

// liquibase command, needed to apply rollbacks in local-dev.
def properties = new Properties()
if (file('src/main/resources/application.properties').exists()) {
    file('src/main/resources/application.properties').withInputStream {
        stream -> properties.load(stream)
    }
}

task backend1Database1Rollback(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = 'liquibase.integration.commandline.Main'
    args '--changeLogFile=database1/liquibase/changelog.xml',
            '--driver=org.mariadb.jdbc.Driver',
            '--url=' + properties.getProperty('quarkus.datasource.database1.jdbc.url'),
            '--username=' + properties.getProperty('quarkus.datasource.database1.username'),
            '--password=' + properties.getProperty('quarkus.datasource.database1.password'),
            'rollback',
            project.properties['rollbackTag'] ? project.properties['rollbackTag'] : ''
}

// publishing
publishing {
    publications {
        maven(MavenPublication) {
            groupId = theGroup
            artifactId = "fk-backend1"
            version = theVersion
            from components.java
        }
    }
}