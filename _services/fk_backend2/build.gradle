plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'io.quarkus'
    id "org.liquibase.gradle" version "2.0.2"
}

dependencies {
    implementation enforcedPlatform(libs.quarkusPlatform)
    implementation project(':_databases:fk_database1')
    implementation project(':_databases:fk_database2')
    implementation project(':_modules:fk_library')
}

// liquibase command, needed to apply rollbacks in local-dev.
def properties = new Properties()
if (file('src/main/resources/application.properties').exists()) {
    file('src/main/resources/application.properties').withInputStream {
        stream -> properties.load(stream)
    }
}

task backend2Database2Rollback(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = 'liquibase.integration.commandline.Main'
    args '--changeLogFile=database2/liquibase/changelog.xml',
            '--driver=org.postgresql.Driver',
            '--url=' + properties.getProperty('quarkus.datasource.database2.jdbc.url'),
            '--username=' + properties.getProperty('quarkus.datasource.database2.username'),
            '--password=' + properties.getProperty('quarkus.datasource.database2.password'),
            'rollback',
            project.properties['rollbackTag'] ? project.properties['rollbackTag'] : ''
}

// publishing
publishing {
    publications {
        maven(MavenPublication) {
            groupId = theGroup
            artifactId = "fk-backend2"
            version = theVersion
            from components.java
        }
    }
}