plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'io.quarkus'
    id "org.liquibase.gradle" version "2.0.2"
}

// dependencies-resolvement
repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    implementation enforcedPlatform(libs.quarkusPlatform)
    implementation project(':fk_product')
    implementation project(':fk_database')

    implementation libs.bundles.quarkus
    implementation libs.bundles.jooq
    implementation libs.bundles.aws
    implementation libs.mariadbJavaClient
    implementation libs.jacksonCsv
    implementation libs.jacksonJsr310

    testImplementation libs.bundles.test.quarkus
    testImplementation libs.bundles.test.testcontainers
    testImplementation libs.awaitility
    testImplementation libs.mariadbJavaClient
    testImplementation libs.restAssured
    testImplementation project(':fk_database')
}

// liquibase command, needed to apply rollbacks in local-dev.
def properties = new Properties()
if (file('src/main/resources/application.properties').exists()) {
    file('src/main/resources/application.properties').withInputStream {
        stream -> properties.load(stream)
    }
}

task liquibaseRollback(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = 'liquibase.integration.commandline.Main'
    args '--changeLogFile=database/liquibase/changelog.xml',
            '--driver=org.mariadb.jdbc.Driver',
            '--url=' + properties.getProperty('quarkus.datasource.jdbc.url'),
            '--username=' + properties.getProperty('quarkus.datasource.username'),
            '--password=' + properties.getProperty('quarkus.datasource.password'),
            'rollback',
            project.properties['rollbackTag'] ? project.properties['rollbackTag'] : ''
}
