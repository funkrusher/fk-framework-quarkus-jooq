plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'org.kordamp.gradle.jandex'
}

// dependencies-resolvement
repositories {
    mavenCentral()
}
dependencies {
    api project(':fk_foundation')
    api libs.bundles.quarkus
    api libs.bundles.jooq
    api libs.bundles.testcontainers
    api libs.mariadbJavaClient
}

// configure generated/src-directories, necessary for use in intellij-idea.
def mySrcDir = "src/main/java"
def generatedDir = "src/main/generated"
sourceSets {
    main {
        java {
            srcDir generatedDir
            srcDir mySrcDir
        }
    }
}
idea {
    module {
        generatedSourceDirs.addAll(file(generatedDir))
        sourceDirs.addAll(file(generatedDir))
        sourceDirs.addAll(file(mySrcDir))
    }
}

// jooqCodeGenerator task
task generateJooqCode(type: JavaExec) {
    group = 'application'
    description = 'Runs JooqCodeGenerator Java Application'
    mainClass = 'org.fk.generator.JooqCodeGenerator'
    classpath = sourceSets.main.runtimeClasspath
    dependsOn("jandex")
}