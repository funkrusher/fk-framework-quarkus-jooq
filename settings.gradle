pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        mavenLocal()
    }
    plugins {
        id "io.quarkus" version '3.8.2'
        id "org.liquibase.gradle" version "2.0.2"
    }
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // versions
            version('fk-core', '1.0.0-SNAPSHOT')
            version('quarkus', '3.8.2')
            version('mariadb-java-client', '3.3.3')
            version('postgres-java-client', '42.7.3')
            version('testcontainers', '1.19.7')
            version('rest-assured', '5.4.0')
            version('awaitility', '4.2.1')
            version('jooq', '3.19.6')
            version('jackson', '2.17.0')
            version('awssdk', '2.25.11')
            version('ulid', '5.2.3')

            // fk-framework self-dependencies (sticky versions)
            // this should mostly be stable core-libraries / shared modules that don't change often.
            library('fkCore', 'org.fk', 'fk-core').versionRef('fk-core')

            // ulid identifiers
            library('ulid', 'com.github.f4b6a3', 'ulid-creator').versionRef('ulid')

            // aws-sdk2
            library('awssdk', 'software.amazon.awssdk', 'bom').versionRef('awssdk')
            library('awssdkCognitoIdentityProvider', 'software.amazon.awssdk', 'cognitoidentityprovider').versionRef('awssdk')

            // jackson (which are not already included in quarkus-jackson
            library('jacksonCsv', 'com.fasterxml.jackson.dataformat', 'jackson-dataformat-csv').versionRef('jackson')
            library('jacksonJsr310', 'com.fasterxml.jackson.datatype', 'jackson-datatype-jsr310').versionRef('jackson')

            // quarkus
            library('quarkusPlatform', 'io.quarkus.platform', 'quarkus-bom').versionRef('quarkus')
            library('quarkusLiquibase', 'io.quarkus', 'quarkus-liquibase').versionRef('quarkus')
            library('quarkusArc', 'io.quarkus', 'quarkus-arc').versionRef('quarkus')
            library('quarkusJdbcMariadb', 'io.quarkus', 'quarkus-jdbc-mariadb').versionRef('quarkus')
            library('quarkusJdbcPostgres', 'io.quarkus', 'quarkus-jdbc-postgresql').versionRef('quarkus')
            library('quarkusResteasyReactiveJackson', 'io.quarkus', 'quarkus-resteasy-reactive-jackson').versionRef('quarkus')
            library('quarkusSmallryeOpenapi', 'io.quarkus', 'quarkus-smallrye-openapi').versionRef('quarkus')
            library('quarkusSmallryeMetrics', 'io.quarkus', 'quarkus-smallrye-metrics').versionRef('quarkus')
            library('quarkusJunit5', 'io.quarkus', 'quarkus-junit5').versionRef('quarkus')
            library('quarkusHibernateValidator', 'io.quarkus', 'quarkus-hibernate-validator').versionRef('quarkus')
            library('quarkusOidc', 'io.quarkus', 'quarkus-oidc').versionRef('quarkus')
            library('quarkusTestSecurity', 'io.quarkus', 'quarkus-test-security').versionRef('quarkus')
            library('quarkusTestSecurityOidc', 'io.quarkus', 'quarkus-test-security-oidc').versionRef('quarkus')
            library('quarkusJunit5Mockito', 'io.quarkus', 'quarkus-junit5-mockito').versionRef('quarkus')
            library('quarkusReactiveMysqlClient', 'io.quarkus', 'quarkus-reactive-mysql-client').versionRef('quarkus')
            library('quarkusNarayanaTransactions', 'io.quarkus', 'quarkus-narayana-jta').versionRef('quarkus')
            library('quarkusQuartz', 'io.quarkus', 'quarkus-quartz').versionRef('quarkus')

            // mariadb-java-client
            library('mariadbJavaClient', 'org.mariadb.jdbc', 'mariadb-java-client').versionRef('mariadb-java-client')

            // postgres-java-client
            library('postgresJavaClient', 'org.postgresql', 'postgresql').versionRef('postgres-java-client')

            // rest-assured
            library('restAssured', 'io.rest-assured', 'rest-assured').versionRef('rest-assured')

            // awaitility
            library('awaitility', 'org.awaitility', 'awaitility').versionRef('awaitility')

            // jooq
            library('jooq', 'org.jooq', 'jooq').versionRef('jooq')
            library('jooqMeta', 'org.jooq', 'jooq-meta').versionRef('jooq')
            library('jooqCodegen', 'org.jooq', 'jooq-codegen').versionRef('jooq')

            // testcontainers
            library('testcontainers', 'org.testcontainers', 'testcontainers').versionRef('testcontainers')
            library('testcontainersMariadb', 'org.testcontainers', 'mariadb').versionRef('testcontainers')
            library('testcontainersPostgres', 'org.testcontainers', 'postgresql').versionRef('testcontainers')
            library('testcontainersJunitJupiter', 'org.testcontainers', 'junit-jupiter').versionRef('testcontainers')

            // bundles
            bundle('quarkus', ['quarkusJdbcMariadb', 'quarkusJdbcPostgres', 'quarkusResteasyReactiveJackson', 'quarkusLiquibase', 'quarkusSmallryeOpenapi', 'quarkusSmallryeMetrics', 'quarkusHibernateValidator', 'quarkusArc', 'quarkusOidc', 'quarkusReactiveMysqlClient', 'quarkusQuartz'])
            bundle('testcontainers', ['testcontainers', 'testcontainersMariadb', 'testcontainersPostgres'])
            bundle('jooq', ['jooq', 'jooqMeta', 'jooqCodegen'])
            bundle('aws', ['awssdk', 'awssdkCognitoIdentityProvider'])

            // test-bundles
            bundle('test.quarkus', ['quarkusJunit5', 'quarkusLiquibase', 'quarkusJdbcMariadb', 'quarkusResteasyReactiveJackson', 'quarkusSmallryeOpenapi', 'quarkusHibernateValidator', 'quarkusArc', 'quarkusTestSecurity', 'quarkusTestSecurityOidc', 'quarkusJunit5Mockito'])
            bundle('test.testcontainers', ['testcontainers', 'testcontainersMariadb', 'testcontainersJunitJupiter'])
        }
    }
}

rootProject.name='fk-framework-quarkus-jooq'
include ':_core:fk_core'
include ':_databases:fk_database1'
include ':_databases:fk_database2'
include ':_modules:fk_product'
include ':_modules:fk_library'
include ':_services:fk_backend1'
include ':_services:fk_backend2'
include ':_services:fk_cognitoLocal'